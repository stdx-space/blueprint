job "${job_name}" {
  datacenters = ["${datacenter_name}"]
  type        = "service"
  namespace   = "${namespace}"

  group "minio" {

%{ for config in host_volume_configs ~}
    volume "minio-data" {
      type      = "host"
      source    = "${config.source}"
      read_only = ${config.read_only}
    }
%{ endfor ~}
%{ for config in dynamic_host_volume_configs ~}
    volume "minio-data" {
      type            = "host"
      source          = "${config.name}"
      access_mode     = "${config.capability.access_mode}"
      attachment_mode = "${config.capability.attachment_mode}"
    }
%{ endfor ~}

    network {
      mode = "bridge"
      port "api" {
        to = 9000
      }

      port "web" {
        to = 9001
      }

%{ for config in https_configs ~}
      port "tls" {
        to = 8443
      }
%{ endfor ~}
    }

    service {
      name = "${job_name}"
      tags = [
        "traefik.enable=true",
        "traefik.http.routers.${job_name}.rule=Host(`${minio_hostname}`)",
        "traefik.http.routers.${job_name}.entrypoints=${traefik_entrypoint.http}",
        "traefik.http.routers.${job_name}-secure.rule=Host(`${minio_hostname}`)",
        "traefik.http.routers.${job_name}-secure.entrypoints=${traefik_entrypoint.https}",
        "traefik.http.routers.${job_name}-secure.tls=true",
%{ for config in consul_connect_service_configs ~}
        "traefik.consulcatalog.connect=true",
%{ endfor ~}
      ]
%{ for config in consul_service_configs ~}
      port = "api"
%{ endfor ~}
%{ for config in nomad_service_configs ~}
      port     = "api"
      provider = "nomad"
%{ endfor ~}
%{ for config in consul_connect_service_configs ~}
      port = 9000

      connect {
        sidecar_service {}
      }
%{ endfor ~}

      check {
        name     = "service: minio healthcheck"
        type     = "http"
        path     = "/minio/health/live"
        interval = "10s"
        timeout  = "2s"
%{ for config in consul_connect_service_configs ~}
        expose   = true
%{ endfor ~}
      }
    }

%{ for config in https_configs ~}
    service {
      name     = "${job_name}-tls"
      port     = "tls"
%{ for config in nomad_service_configs ~}
      provider = "nomad"
%{ endfor ~}
    }
%{ endfor ~}

    task "minio" {
      driver = "docker"

      config {
        image = "minio/minio:latest"
        args  = ["server", "/data", "--console-address", ":9001"]
      }

      template {
        data = <<-EOT
        MINIO_ROOT_USER = "${minio_user}"
        MINIO_ROOT_PASSWORD = "${minio_password}"
        EOT
        destination = "$${NOMAD_SECRETS_DIR}/.env"
        env = true
      }

      resources {
        cpu    = ${resources.cpu}
        memory = ${resources.memory}
      }
%{ for config in host_volume_configs ~}
      volume_mount {
        volume      = "minio-data"
        destination = "/data"
        read_only   = ${config.read_only}
      }
%{ endfor ~}
%{ for config in dynamic_host_volume_configs ~}
      volume_mount {
        volume      = "minio-data"
        destination = "/data"
      }
%{ endfor ~}
    }

    task "minio-initialize-buckets" {
      lifecycle {
        hook    = "poststart"
        sidecar = false
      }
      driver = "docker"

      config {
        image = "alpine/ansible:latest"
        command = "ansible-playbook"
        args  = ["local/initialize-buckets.yaml"]
      }

      template {
        data        = <<-EOF
${initialize_buckets_playbook}
EOF
        destination     = "local/initialize-buckets.yaml"
        left_delimiter  = "<<"
        right_delimiter = ">>"
      }

      template {
        data        = <<-EOF
${initialize_buckets_vars}
EOF
        destination = "local/initialize-buckets-vars.yaml"
      }
    }

%{ for config in https_configs ~}
    task "minio-tls-proxy" {
      driver = "docker"

      lifecycle {
        hook    = "poststart"
        sidecar = true
      }

      config {
        image = "caddy:2"

        args = [
          "caddy",
          "run",
          "--config",
          "/local/conf/Caddyfile",
        ]
      }

      template {
        # https://caddy.community/t/https-on-a-local-ip-address-with-docker/11096/5
        data = <<EOH
:8443 {
    reverse_proxy :9000
    tls internal {
        on_demand
    }
}
EOH
        destination = "local/conf/Caddyfile"
      }
    }
%{ endfor ~}
  }
}
